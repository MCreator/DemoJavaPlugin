plugins {
    id "java"
}

repositories {
    mavenCentral()
    maven { url "https://repo.gradle.org/gradle/libs-releases" } // for gradle-tooling-api
    flatDir { dirs mcreator_path + '/lib' }
}

group = 'net.mcreator.demojavaplugin'
version = '1.0.0'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

dependencies {
    implementation project(':MCreator')

    // Due to Gradle limitations, you need to declare libraries you need but MCreator already provides here
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.23.1'
}

tasks.jar {
    archiveFileName.set("demo-plugin.zip")
}

tasks.register('runMCreatorWithPlugin', JavaExec) {
    dependsOn jar

    environment("MCREATOR_PLUGINS_FOLDER", file("./build/libs"))
    environment("MCREATOR_PLUGINS_DEV", "")

    jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'

    classpath = project(':MCreator').sourceSets.main.runtimeClasspath

    main = 'net.mcreator.Launcher'

    workingDir = mcreator_path
}

subprojects { subproject ->
    subproject.plugins.withType(JavaPlugin) {
        test {
            dependsOn rootProject.jar

            environment("MCREATOR_PLUGINS_FOLDER", new File(rootProject.projectDir, "/build/libs"))
            environment("MCREATOR_PLUGINS_DEV", "")

            workingDir = mcreator_path
        }
    }
}